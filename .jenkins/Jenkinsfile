// The main pipeline.
node(getNodeLabel()) {
  skipDefaultCheckout() // I hope this works!
  stage('test') {
    timeout(600) {
      ansiColor('xterm') {
        timestamps {
          try {
            def scmVars = checkout() // see above!
            doBuild(scmVars)
          } finally {
            try {
              addCDashBadge()
              if ("${env.JOB_NAME}".contains("continuous") || "${env.JOB_NAME}".contains("nightly")) {
                emailFailureResults()
              }
            } finally {
              cleanWorkspace()
            }
          }
        }
      }
    }
  }
}

def getNodeLabel() {
  def pattern = ~/^((linux|mac-arm)-[a-z]+(?:-unprovisioned)?)/
  def match = "${env.JOB_NAME}" =~ pattern
  
  if (match.find()) {
    return match.group(1)
  }
  else {
    return null
  }
}

def checkout(String drakeSha = "master", String ciSha = "main") {
  def scmVars = null
  retry(4) {
    scmVars = checkout([$class: 'GitSCM', branches: [[name: '${drakeSha}']], extensions: [[$class: 'AuthorInChangelog'], [$class: 'CloneOption', honorRefspec: true, noTags: true], [$class: 'RelativeTargetDirectory', relativeTargetDir: 'src'], [$class: 'LocalBranch', localBranch: 'master']], userRemoteConfigs: [[credentialsId: 'ad794d10-9bc8-4a7a-a2f3-998af802cab0', name: 'origin', refspec: '+refs/heads/master:refs/remotes/origin/master', url: 'git@github.com:RobotLocomotion/drake.git']]])
  }
  retry(4) {
    checkout([$class: 'GitSCM', branches: [[name: '${ciSha}']], extensions: [[$class: 'AuthorInChangelog'], [$class: 'CloneOption', honorRefspec: true, noTags: true], [$class: 'RelativeTargetDirectory', relativeTargetDir: 'ci'], [$class: 'LocalBranch', localBranch: 'main']], userRemoteConfigs: [[credentialsId: 'ad794d10-9bc8-4a7a-a2f3-998af802cab0', name: 'origin', refspec: '+refs/heads/main:refs/remotes/origin/main', url: 'git@github.com:RobotLocomotion/drake-ci.git']]])
  }
  return scmVars
}

def doBuild(Map scmVars) {
  withCredentials([
    sshUserPrivateKey(credentialsId: 'ad794d10-9bc8-4a7a-a2f3-998af802cab0', keyFileVariable: 'SSH_PRIVATE_KEY_FILE'),
    string(credentialsId: 'e21b9517-8aa7-419e-8f25-19cd42e10f68', variable: 'DOCKER_USERNAME'),
    file(credentialsId: '912dd413-d419-4760-b7ab-c132ab9e7c5e', variable: 'DOCKER_PASSWORD_FILE')
  ]) {
    withEnv(["GIT_COMMIT=${scmVars.GIT_COMMIT}"]) {
      sh "${env.WORKSPACE}/ci/ctest_driver_script_wrapper.bash"
    }
  }
}

def emailFailureResults() {
  if (fileExists('RESULT')) {
    currentBuild.result = readFile 'RESULT'
  }
  if (currentBuild.result == 'FAILURE' || currentBuild.result == 'UNSTABLE') {
    def subject = 'Build failed in Jenkins'
    if (currentBuild.result == 'UNSTABLE') {
      subject = 'Jenkins build is unstable'
    }
    emailext (
      subject: "${subject}: ${env.JOB_NAME} #${env.BUILD_NUMBER}",
      body: "See <${env.BUILD_URL}display/redirect?page=changes> and <${env.BUILD_URL}changes>",
      to: '$DEFAULT_RECIPIENTS',
    )
  }
}

def addCDashBadge() {
  if (fileExists('CDASH')) {
    def cDashUrl = readFile 'CDASH'
    addBadge icon: '/userContent/cdash.png', link: cDashUrl, text: 'View in CDash'
    addSummary icon: '/userContent/cdash.png', link: cDashUrl, text: 'View in CDash'
  }
}

def cleanWorkspace() {
  deleteDir()
  dir("${env.WORKSPACE}@tmp") {
    deleteDir()
  }
}
